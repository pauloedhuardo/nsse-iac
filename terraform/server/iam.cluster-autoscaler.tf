data "aws_iam_policy_document" "cluster_autoscaler" {
  statement {
    effect = "Allow"

    resources = ["*"]
    actions = [
      "autoscaling:DescribeAutoScalingGroups",
      "autoscaling:DescribeAutoScalingInstances",
      "autoscaling:DescribeLaunchConfigurations",
      "autoscaling:DescribeScalingActivities",
      "ec2:DescribeImages",
      "ec2:DescribeInstanceTypes",
      "ec2:DescribeLaunchTemplateVersions",
      "ec2:GetInstanceTypesFromInstanceRequirements"
    ]
  }

  statement {
    effect = "Allow"

    resources = [module.ec2_worker_instance.auto_scaling_group_arn]
    actions = [
      "autoscaling:SetDesiredCapacity",
      "autoscaling:TerminateInstanceInAutoScalingGroup"
    ]
  }
}

resource "aws_iam_policy" "cluster_autoscaler" {
  name   = var.worker_auto_scaling_group.cluster_autoscaler_policy_name
  policy = data.aws_iam_policy_document.cluster_autoscaler.json
}

resource "aws_iam_role_policy_attachment" "cluster_autoscaler_policy_attachment" {
  role       = aws_iam_role.instance_role.name
  policy_arn = aws_iam_policy.cluster_autoscaler.arn
}